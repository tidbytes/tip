program = function+

function = identifier arg-list <b-open> statements return <b-close>

arg-list = <p-open> args <p-close>

<args> = identifier? (<comma> identifier)*

expr-list = <p-open> expr? (<comma> expr)* <p-close>

var-decl = <'var'> args <semi-c>

<expr> = constant | identifier | comparison | calculation | invocation | input
       | null | dynamic-call | function-call | malloc

null = <'null'>
input = <'input'>
malloc = <'malloc'>

dynamic-call = <p-open> expr <p-close> expr-list
function-call = identifier expr-list

<comparison> = equals
             | greater-than
             | less-than

equals = expr <'=='> expr
greater-than = expr <'>'> expr
less-than = expr <'<'> expr

<calculation> = addition
              | subtraction
              | multiplication
              | division

addition = expr <'+'> expr
subtraction = expr <'-'> expr
multiplication = expr <'*'> expr
division = expr <'/'> expr

<statement> = assignment
            | output
            | if-statement
            | if-statement else-statement
            | while
            | var-decl

assignment = identifier <'='> expr <semi-c>

output = <'output'> expr <semi-c>

while = <'while'> <p-open> expr <p-close> statement
      | <'while'> <p-open> expr <p-close> <b-open> statements <b-close>

if-statement = <'if'> <p-open> expr <p-close> statement
             | <'if'> <p-open> expr <p-close> <b-open> statements <b-close>
else-statement = <'else'> statement
               | <'else'> <b-open> statements <b-close>

<statements> = statement*

invocation = <p-open> expr <p-close>
scoped-statement = statement
                 | <b-open> statements <b-close>

return = <'return'> expr <semi-c>;

binop = '+' | '-' | '*' | '/' | '>' | '=='

whitespace = #"\s+"
identifier = #"[a-zA-Z]+" | '*' identifier
constant = '0' | #"[1-9][0-9]*"

p-open = '('
p-close = ')'
b-open = '{'
b-close = '}'
comma = ','
semi-c = ';'